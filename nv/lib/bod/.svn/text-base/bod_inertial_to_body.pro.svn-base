;===========================================================================
; bod_inertial_to_body
;
; v is array (nv,3,nt) of 3-element column vectors.  bdp is array of nt
; body descriptors.  result is array (nv,3,nt) of 3-element
; column vectors.
;
;===========================================================================
function bod_inertial_to_body, bxp, v
@nv_lib.include
 bdp = class_extract(bxp, 'BODY')
 bd = nv_dereference(bdp)

 nt = n_elements(bd)
 sv = size(v)
 nv = sv[1]

 sub = linegen3x(nv,3,nt)

 M0 = (bd.orientT[*,0,*])[sub]
 M1 = (bd.orientT[*,1,*])[sub]
 M2 = (bd.orientT[*,2,*])[sub]

 r = dblarr(nv,3,nt,/nozero)
 r[*,0,*] = total(M0*v,2)
 r[*,1,*] = total(M1*v,2)
 r[*,2,*] = total(M2*v,2)

 return, r
end
;===========================================================================
