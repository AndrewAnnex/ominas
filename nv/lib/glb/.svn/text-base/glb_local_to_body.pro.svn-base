;=============================================================================
; glb_local_to_body
;
;  v is a point on the surface in the body frame, r is a direction from that 
;  point in the local frame.
;
;  Local coordinate system is cartesian w axis 0 toward the east, 1 north, and
;  2 zenith.
;
;=============================================================================
function glb_local_to_body, gbxp, v, r
@nv_lib.include
 gbdp = class_extract(gbxp, 'GLOBE')
 gbd = nv_dereference(gbdp)

 sv = size(v)
 nv = sv[1]
 nt = n_elements(gbd)

 z = dblarr(nv,3,nt) & z[*,2,*] = 1d

 zenith = glb_surface_normal(gbdp, v)
 east = v_unit(v_cross(z, zenith))
 north = v_cross(zenith, east)

 M = dblarr(nv,3,3,nt)
 M[*,0,*,*] = east
 M[*,1,*,*] = north
 M[*,2,*,*] = zenith


 result = dblarr(nv,3,nt, /nozero)
 result[*,0,*] = v_inner(M[*,*,0,*],r)
 result[*,1,*] = v_inner(M[*,*,1,*],r)
 result[*,2,*] = v_inner(M[*,*,2,*],r)

 return, result
end
;=============================================================================
