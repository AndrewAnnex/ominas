;=============================================================================
;+
; NAME:
;       disk_to_image
;
;
; PURPOSE:
;       Translates a point in disk coordinates to image coordinates
;
;
; CATEGORY:
;       NV/LIB/TOOLS
;
;
; CALLING SEQUENCE:
;       result = disk_to_image(cd, dkx, p)
;
;
; ARGUMENTS:
;  INPUT:
;             cd:       Camera or map descriptor
;
;             dkx:      Object descriptor (subclass of DISK)
;
;              p:       An array of image points
;
;  OUTPUT:
;       NONE
;
; KEYWORDS:
;   INPUT:
;		sund:	If given, longitudes are assumed to be referenced to the 
;			sun direction.
;
;
; RETURN:
;       An array of image coordinates.
;
; STATUS:
;       Completed.
;
;
; MODIFICATION HISTORY:
;       Written by:     Spitale, 9/2002
;-
;=============================================================================
function  disk_to_image, cd, dkx, p, body_pts=p_body, frame_bd=frame_bd, valid=valid

 nt = n_elements(cd)
 sv = size(p)
 nv = sv[1]

 class = class_get(cd[0])

 case class of 
  'MAP' : return, map_map_to_image(cd, disk_to_map(cd, dkx, p), valid=valid)

  'CAMERA' : $
	begin
	 p_body = dsk_disk_to_body(dkx, p, frame_bd=frame_bd)
	 image_pts =  inertial_to_image_pos(cd,  $
                        bod_body_to_inertial_pos(dkx, p_body))
	 valid = in_image(cd, image_pts, slop=0)
	 return, image_pts
	end

  default :
 endcase

end
;==========================================================================
