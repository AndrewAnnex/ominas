;=============================================================================
;+
; NAME:
;       body_to_image_pos
;
;
; PURPOSE:
;       Translates a vector in body coordinates to image coordinates
;
;
; CATEGORY:
;       NV/LIB/TOOLS
;
;
; CALLING SEQUENCE:
;       result = body_to_image_pos(cd, od, v, inertial=inertial)
;
;
; ARGUMENTS:
;  INPUT:
;             cd:       Camera or map descriptor
;
;             od:       Object descriptor
;
;              v:       An array of position vectors
;
;  OUTPUT:
;	NONE
;
; KEYWORDS:
;  INPUT:
;	NONE
;
;  OUTPUT:
;       inertial:       Vectors in inertial coordinates.
;
;
; RETURN:
;       An array of vectors in image coordinates.
;
; STATUS:
;       Completed.
;
;
; MODIFICATION HISTORY:
;       Written by:     Spitale
;
;-
;=============================================================================
function body_to_image_pos, cd, od, v, inertial=inertial, valid=valid, frame_bd=frame_bd

 class = (class_get(cd))[0]

 case class of 
  'MAP' : return, surface_to_image(cd, od, frame_bd=frame_bd, valid=valid, $
	            body_to_surface(od, v, frame_bd=frame_bd))

  'CAMERA' : $
	begin
	 bd = class_extract(od, 'BODY')

	 inertial = bod_body_to_inertial_pos(bd, v)
	 return, cam_focal_to_image(cd, $
		   cam_body_to_focal(cd, $
		     bod_inertial_to_body_pos(cam_body(cd), inertial)))
	end


  else :
 endcase


end
;==========================================================================
