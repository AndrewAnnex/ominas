;=============================================================================
; get_ring_profile_outline_perp
;
;=============================================================================
function get_ring_profile_outline_perp, cd, dkx, points, $
       xlon=xlon, dir=dir, nrad=nrad, nlon=nlon, frame_bd=frame_bd

; see get_ring_profile_outline to solve wrap-around problem

 ;------------------------------------------------
 ; get coords of corners
 ;------------------------------------------------
 dsk_pts = image_to_disk(cd, dkx, fr=frame_bd, points)
 dsk_pt0 = dsk_pts[0,*]
 dsk_pt = dsk_pts[1,*]

 ;- - - - - - - - - - - - - - - - - - - -
 ; initial point
 ;- - - - - - - - - - - - - - - - - - - -
 if(keyword_set(points)) then base0 = points[*,0] $
 else base0 = reform(disk_to_image(cd, dkx, dsk_pt0, frame_bd=frame_bd))
 dsk_pt_base0 = dsk_pt0
 lon_base0 = dsk_pt_base0[1]
 rad_base = dsk_pt_base0[0]		; = dsk_pt_base1[0]

 ;- - - - - - - - - - - - - - - - - - - -
 ; current point
 ;- - - - - - - - - - - - - - - - - - - -
 if(keyword_set(points)) then curr = points[*,1] $
 else curr = reform(disk_to_image(cd, dkx, dsk_pt, frame_bd=frame_bd))
 top1 = curr
 dsk_pt_top1 = dsk_pt
 lon_top1 = dsk_pt_top1[1]

 ;- - - - - - - - - - - - - - - - - - - -
 ; the point "radial" from base0
 ;- - - - - - - - - - - - - - - - - - - -
 top0 = reform(get_radperp(cd, dkx, base0, curr, dsk_pt0, frame_bd=frame_bd))
 dsk_pt_top0 = image_to_disk(cd, dkx, top0, frame_bd=frame_bd)
 lon_top0 = dsk_pt_top0[1]
 rad_top = dsk_pt_top0[0]			; = dsk_pt_top1[0]

 ;- - - - - - - - - - - - - - - - - - - -
 ; the point "azimuthal" from base0
 ;- - - - - - - - - - - - - - - - - - - -
 dlon = lon_top1 - lon_top0
 lon_base1 = lon_base0 + dlon
 dsk_pt_base1 = tr([rad_base, lon_base1, 0d])
 base1 = reform(disk_to_image(cd, dkx, dsk_pt_base1, frame_bd=frame_bd))

 ;------------------------------------------------
 ; generate sides of outline
 ;------------------------------------------------
 lon_pts_top = dindgen(nlon)/(nlon-1)*dlon + lon_top0
 lon_pts_base = dindgen(nlon)/(nlon-1)*dlon + lon_base0

 dsk_pts_top = tr([tr(make_array(nlon, val=rad_top)), $
                  tr(lon_pts_top), $
                  tr(make_array(nlon, val=0d))])

 dsk_pts_base = tr([tr(make_array(nlon, val=rad_base)), $
                     tr(lon_pts_base), $
                     tr(make_array(nlon, val=0d))])

 im_pts_lon_top = reform(disk_to_image(cd, dkx, dsk_pts_top, frame_bd=frame_bd))
 im_pts_lon_base = reform(disk_to_image(cd, dkx, dsk_pts_base, frame_bd=frame_bd))

 im_pts_rad_0 = [tr(dindgen(nrad)/(nrad-1)*(top0[0]-base0[0]) + base0[0]), $
                 tr(dindgen(nrad)/(nrad-1)*(top0[1]-base0[1]) + base0[1])]

 dsk_pts_rad_0 = image_to_disk(cd, dkx, im_pts_rad_0, frame_bd=frame_bd)
 lon_pts_rad_0 = dsk_pts_rad_0[*,1]
 rad_pts_rad_0 = dsk_pts_rad_0[*,0]
 lon_pts_rad_1 = lon_pts_rad_0 + (lon_base1 - lon_base0)
 dsk_pts_rad_1 = dblarr(nrad,3)
 dsk_pts_rad_1[*,0] = rad_pts_rad_0 
 dsk_pts_rad_1[*,1] = lon_pts_rad_1 
 im_pts_rad_1 = disk_to_image(cd, dkx, dsk_pts_rad_1, frame_bd=frame_bd)


 ;------------------------------------------------
 ; construct outline
 ;------------------------------------------------
 outline_pts = dblarr(2,2*(nlon+nrad))
 outline_pts[0,*] = [tr(im_pts_lon_base[0,*]), $
                     tr(im_pts_rad_1[0,*]), $
                     tr(rotate(im_pts_lon_top[0,*],2)), $
                     tr(rotate(im_pts_rad_0[0,*],2))]
 outline_pts[1,*] = [tr(im_pts_lon_base[1,*]), $
                     tr(im_pts_rad_1[1,*]), $
                     tr(rotate(im_pts_lon_top[1,*],2)), $
                     tr(rotate(im_pts_rad_0[1,*],2))]


 return, outline_pts
end
;===========================================================================



