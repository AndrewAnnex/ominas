;===========================================================================
; image_northangle
;
;  Computes the image azimuth (see image_azimuth.pro) of the north direction 
;  on the surface of the given body at the specified pixel location p.
;
;===========================================================================
function image_northangle, cd, gbx, p, valid=valid

 valid = -1

 zz = tr([0,0,1d])

 surf_pt = image_to_surface(cd, gbx, p, body=body_pt, dis=dis)
 if(dis[0] LT 0) then return, 0
 valid = 0

 body_pt = body_pt[0,*]
 nn = v_unit(glb_surface_normal(gbx, body_pt))
 ll = v_unit(v_cross(zz, nn))

 northaz = v_cross(nn, ll)
 northaz_inertial = bod_body_to_inertial(gbx, northaz)

 return, image_azimuth(cd, northaz_inertial)
end
;===========================================================================



