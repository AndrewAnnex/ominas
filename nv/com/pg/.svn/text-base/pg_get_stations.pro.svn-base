;=============================================================================
;+
; NAME:
;	pg_get_stations
;
;
; PURPOSE:
;	Obtains a station descriptor for the given data descriptor.  
;
;
; CATEGORY:
;	NV/PG
;
;
;
;
; MODIFICATION HISTORY:
; 	Written by:	Spitale, 2/2009
;	
;-
;=============================================================================
function pg_get_stations, dd, trs, od=od, pd=pd, gd=gd, no_sort=no_sort, $
                          override=override, verbatim=verbatim, $
@station_keywords.include
@nv_trs_keywords_include.pro
		end_keywords

 ;-----------------------------------------------
 ; dereference the generic descriptor if given
 ;-----------------------------------------------
 pgs_gd, gd, od=od, pd=pd, dd=dd


 ;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 ; if names requested, the force tr_first
 ;- - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
 if(keyword_set(stn__name)) then tr_first = 1
;tr_first = 1

 stds = nv_get_value(dd, 'STN_DESCRIPTORS', key1=od, key2=pd, $
                             key7=stn__time, key8=stn__name, trs=trs, $
@nv_trs_keywords_include.pro
	end_keywords)

 if(NOT keyword_set(stds)) then return, ptr_new()

 n = n_elements(stds)

 ;---------------------------------------------------
 ; If stn__name given, determine subscripts such that
 ; only values of the named objects are returned.
 ;
 ; Note that each translator has this opportunity,
 ; but this code guarantees that it is done.
 ;
 ; If stn_name is not given, then all descriptors
 ; will be returned.
 ;---------------------------------------------------
 if(keyword__set(stn__name)) then $
  begin
   tr_names = get_core_name(stds)
   sub = nwhere(tr_names, stn__name)
   if(sub[0] EQ -1) then return, ptr_new()
   if(NOT keyword__set(verbatim)) then sub = sub[sort(sub)]
  end $
 else sub=lindgen(n)

 n = n_elements(sub)
 stds = stds[sub]


 ;------------------------------------------------------------
 ; Make sure that for a given name, only the first 
 ; descriptor obtained from the translators is returned.
 ; Thus, translators can be arranged in order in the table
 ; such the the first occurence has the highest priority.
 ;------------------------------------------------------------
 if(NOT keyword_set(no_sort)) then stds = stds[pgs_name_sort(get_core_name(stds))]


 return, stds
end
;===========================================================================








